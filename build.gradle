import org.apache.tools.ant.filters.ReplaceTokens

import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'com.diffplug.spotless' version '6.18.0'
}

group 'ie.francis'
version '0.0.1-alpha'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.ow2.asm', name: 'asm', version: '9.5'
    implementation group: 'org.ow2.asm', name: 'asm-commons', version: '9.5'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

}

test {
    useJUnitPlatform()
}

jar {

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes(
                'Main-Class': 'ie.francis.lisp.Main'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

compileJava {
    doFirst {
        File f = new File("${project.layout.buildDirectory.get()}/generated-src/main/java/ie/francis/lisp/Version.java")
        f.text = f.text.replace("@VERSION_STRING@", "$version (${new SimpleDateFormat("dd MMM yyyy, HH:mm:ss").format(new Date())})")
    }
    doFirst {
        copy {
            from "src/main/java/ie/francis/lisp/Version.java.tmpl"
            into "${project.layout.buildDirectory.get()}/generated-src/main/java/ie/francis/lisp"
            rename("Version.java.tmpl", "Version.java")
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'build/generated-src/main/java']
        }
    }
}

spotless {
    String header = '''/*
 * (c) 2023 Francis McNamee
 * */
''' + "\n"

    java {
        ratchetFrom 'origin/main'
        googleJavaFormat()
        licenseHeader header
        target 'src/main/java/**/*.java'
    }

    // This avoids failing every gradle build if spotlessApply hasn't been ran yet
    enforceCheck false
}
